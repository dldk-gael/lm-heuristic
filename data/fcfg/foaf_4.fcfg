# Multiple facts
Facts[head=?fact, tail=na] -> Fact[data=?fact, grammar=[subj=arg0, -no_subj]]
Facts[head=?fact, tail=na] -> Fact[data=?fact, grammar=[subj=arg1, -no_subj]]

Facts[head=?fact, tail=[head=?fact2, tail=na]] -> Fact[data=?fact, grammar=[subj=arg0, -no_subj]] conj Fact[data=?fact2, grammar=[subj=arg0]]
Facts[head=?fact, tail=[head=?fact2, tail=na]] -> Fact[data=?fact, grammar=[subj=arg0, -no_subj]] conj Fact[data=?fact2, grammar=[subj=arg1, +explicit_subj]]
Facts[head=?fact, tail=[head=?fact2, tail=na]] -> Fact[data=?fact, grammar=[subj=arg1, -no_subj]] conj Fact[data=?fact2, grammar=[subj=arg1]]
Facts[head=?fact, tail=[head=?fact2, tail=na]] -> Fact[data=?fact, grammar=[subj=arg1, -no_subj]] conj Fact[data=?fact2, grammar=[subj=arg0, +explicit_subj]]

# Two facts with same arg0 
Facts[head=[arg0=?arg0, pred=?pred1, arg1=?arg1], \
      tail=[head=[arg0=?arg0, pred=?pred2, arg1=?arg2], tail=na]] -> \
      M_Args[data=?arg0] "," Relative Fact[data=[arg0=?arg0, pred=?pred1, arg1=?arg1], grammar=[subj=arg0, +no_subj]] "," Fact[data=[arg0=?arg0, pred=?pred2, arg1=?arg2], grammar=[subj=arg0, +no_subj]] 

# Fact
Fact[data=[arg0=?arg0, pred=?pred, arg1=?arg1], grammar=[subj=?arg0, -explicit_subj, +no_subj]] -> VP[data=?pred, grammar=[subj=?arg0]] M_Args[data=?arg1]
Fact[data=[arg0=?arg0, pred=?pred, arg1=?arg1], grammar=[subj=arg0, explicit_subj=?exp, -no_subj]]-> M_Args[data=?arg0, grammar=[explicit=?exp]] VP[data=?pred, grammar=[subj=arg0]] M_Args[data=?arg1]
Fact[data=[arg0=?arg0, pred=?pred, arg1=?arg1], grammar=[subj=arg1, explicit_subj=?exp, -no_subj]] -> M_Args[data=?arg1, grammar=[explicit=?exp]] VP[data=?pred, grammar=[subj=arg1]] M_Args[data=?arg0]

# Multiple arguments 
M_Args[data=[+empty]] -> ' '
M_Args[data=[-empty, head=?arg, tail=na], grammar=?grammar] -> NP[data=?arg, grammar=?grammar]
M_Args[data=[-empty, head=?arg0, tail=[head=?arg1, tail=?tail]], grammar=?grammar] -> NP[data=?arg0, grammar=?grammar] conj M_Args[data=[-empty, head=?arg1, tail=?tail], grammar=?grammar]
M_Args[data=[-empty, head=?arg0, tail=[head=?arg1, tail=?tail]], grammar=[-explicit]] -> ProN[form=plural]

# Generic rules
NP[data=[gender=?gender, form=?form], grammar=[-explicit]] -> ProN[gender=?gender, form=?form] 
NP[data=[proper=?proper, +is_proper], grammar=[+explicit]] -> ProperName[proper=?proper]
NP[data=[+is_noun, noun=?noun, gender=?gender, form=?form], grammar=[+explicit]] -> Noun[sem=?noun, form=?form] | Det[form=?form] Noun[sem=?noun, form=?form]

# Domaine specific for FOAF 
VP[data=[sem=know_r, form=?form], grammar=[subj=arg0]] -> Verb[sem=know, form=plural] ProN[form=symmetrical] 
VP[data=[sem=know, form=?form], grammar=[subj=arg0]] -> Verb[sem=know, form=?form]
VP[data=[sem=know, form=?form], grammar=[subj=arg1]] -> Verb[sem=be, form=?form] Verb[sem=know, form=pp] "by"
VP[data=[sem=member, form=?form], grammar=[subj=arg1]] -> Verb[sem=include, form=?form]
VP[data=[sem=member, form=?form], grammar=[subj=arg0]] -> Verb[sem=be, form=?form] NP[data=[-is_proper, +is_noun, noun=member], grammar=[+explicit]] "of" 
VP[data=[sem=webblog, form=?form], grammar=[subj=arg0]] -> Verb[sem=have, form=?form] | Verb[sem=own, form=?form]
VP[data=[sem=homepage, form=?form], grammar=[subj=arg0]] -> Verb[sem=have, form=?form] | Verb[sem=own, form=?form]


# Lexicon 
Verb[sem=know, form=pp] -> 'known'
Verb[sem=know, form=plural] -> 'know' 
Verb[sem=know, form=singular] -> 'knows'
Verb[sem=include] -> 'include'
Verb[sem=have, form=singular] -> 'has'
Verb[sem=have, form=plural] -> 'have'
Verb[sem=be, form=singular] -> 'is'
Verb[sem=be, form=plural] -> 'are' 
Verb[sem=own, form=singular] -> 'owns'
Verb[sem=own, form=plural] -> 'own'


ProN[gender=neuter,form=singular] -> 'it'
ProN[form=plural] -> 'they' | 'them' | 'both'
ProN[gender=male,form=singular] -> 'he' | 'him'
ProN[gender=female,form=singular] -> 'she' | 'her'
ProN[form=symmetrical] -> 'each' 'other'

Noun[sem=nickname] -> 'nick' | 'nickname'
Noun[sem=homepage] -> 'homepage' | 'personnal website'
Noun[sem=blog] -> 'blog' 
Noun[sem=know] -> 'acquaintance'
Noun[sem=member] -> 'member' 
Noun[sem=empty] -> ' '
Noun[sem=man, form=singular] -> 'man'
Noun[sem=man, form=plural] -> 'men'

Det[form=singular] -> 'a' | 'an' 
Det[form=plural] -> 'both'
Det -> 'the'

conj -> 'and'

Relative -> 'who' | 'which' | 'that'